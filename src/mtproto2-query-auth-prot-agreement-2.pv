(*
 * MTProto 2.0: Creating an authorization key.
 *
 * Model for the protocol used to create long-term keys shared between a client
 * and Telegram's server.
 *
 * See: https://core.telegram.org/mtproto/auth_key
 *
 * Expected time to run this script: ~3min
 *)

set preciseActions = true.

(*
 * Secrecy assumption: the private key of the server is secure.
 *
 * NOTE: Commenting out this assumption and adding the RevealRSAKey() process
 * significantly increases the verification time (up to a couple of hours). The
 * query still holds true, provided that the last disjunct (RevealedRSAKey()
 * event) is also uncommented out.
 *)
not attacker(new sk).

(*
 * Key agreement: If client and server end up negotiating an authorization key
 * in a session executed correctly, then the key is the same.
 *
 * If the DH exponents are revealed, the attacker can of course compute the
 * authorization key, but she cannot force the client and the server to compute
 * two different keys.
 *)
query nc, ns, nk, nk': Nonce, p, p': Modulus, g, g', g_a, g_b: G, a, b: Z, k, k': AuthKey, sk: PrivKey, pID, pID': ProcessID;
  event   (ServerAcceptsAuthKey(nc, ns, nk,  p,  g,  g_a, a, k))
  && event(ClientAcceptsAuthKey(nc, ns, nk', p', g', g_b, b, k'))
  ==> k = k' && nk = nk' && p = p' && g = g'
  || (event(ServerGeneratesNonce(pID, ns)) && event(ServerGeneratesNonce(pID', ns)) && pID <> pID')
  ||  event(RevealedNk(nk'))
  (* ||  event(RevealedRSAKey(sk)) *)
  .
(* ✔︎ Expected result: true *)


(* Main *)
process
  let allowWeakDiffieHellman = true in
  (0
    | !RegisterRSAKey(Server)
    | !ServerAuth(Server)
    | !ClientAuth(Server, allowWeakDiffieHellman)
    | !GenGoodDHParameters()
    | !GenBadDHParameters()

    (* Allow the attacker to reveal (part of) the client's ephemeral state *)
    | !RevealNk()
    | !RevealAuthProtClientExponent()

    (* Allow the attacker to learn any authorization key *)
    | !RevealAuthKey()

    (* | !RevealRSAKey() *)

    (* Allow the attacker to reveal the server's ephemeral key (the DH exponent) *)
    | !RevealAuthProtServerExponent()
  )

