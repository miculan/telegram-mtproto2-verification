(*
 * MTProto 2.0: Rekeying
 *
 * Model for the protocol used for updating the session keys of secret chats.
 *
 * See: https://core.telegram.org/api/end-to-end/pfs
 *)

(******************************************************************************)
(* Messages exchanged during the rekeying protocol                            *)
(******************************************************************************)
fun HALFKEYA(ChatID, G)                    : Plaintext [data].
fun HALFKEYB(ChatID, G, HashValue)         : Plaintext [data].
fun FPACK   (ChatID, HashValue)            : Plaintext [data].

free iRekeyingText: bitstring [private].
free rRekeyingText: bitstring [private].


(******************************************************************************)
(* "Abstract" cryptographic primitives                                        *)
(*                                                                            *)
(* NOTE: These MUST be defined somewhere before this point. They are          *)
(* not defined here so that the choice of a model for MTProto's encryption    *)
(* scheme can be done independently, without touching this file.              *)
(******************************************************************************)

(* SessionKey → HashValue *)
(* fps(SessionKey) *)

(* Plaintext × SessionKey × AuthKey → Message *)
(* encodeSecretChatMsg(Plaintext, SessionKey, AuthKey) *)

(* Message × SessionKey × AuthKey → Plaintext *)
(* decodeSecretChatMsg(Message, SessionKey, AuthKey) *)


(******************************************************************************)
(* Events                                                                     *)
(******************************************************************************)

(*
 * InitiatorSendsHalfKey(i, A, B, g_a):
 *
 * "A starts a rekeying session i with B by sending a new half-key g_a."
 *)
event InitiatorSendsHalfKey(ChatID, Principal, Principal, G).

(*
 * InitiatorReceivesHalfKey(i, A, B, g_b, fp):
 *
 * "A has received B's half-key g_b and the new session key's fingerprint fp
 * during rekeying session i."
 *)
event InitiatorReceivesHalfKey(ChatID, Principal, Principal, G, HashValue).

(*
 * RekeyingKeyFingerprintMatches(i, A, B, b):
 *
 * "Chat's initiator A (b: 'validates' : 'does not validate') the key
 * fingerprint received from B during rekeying session i."
 *)
event RekeyingKeyFingerprintMatches(ChatID, Principal, Principal, bool).

(*
 * InitiatorHasNegotiatedNewKey(i, A, B, k):
 *
 * "Chat's initiator A has computed a new session key k in chat session i,
 * which she believes to be shared with B."
 *)
event InitiatorHasNegotiatedNewKey(ChatID, Principal, Principal, SessionKey).

(*
 * InitiatorSendsAck(i, A, B, fp):
 *
 * "A sends its new session key's fingerprint fp as an acknowledgment to B in
 * session i."
 *)
event InitiatorSendsAck(ChatID, Principal, Principal, HashValue).

(*
 * ClientSendsMsgWithNewKey(p, X, i, A, B, k, m):
 *
 * "Client X (one of A or B) running process p in rekeying session i sends a secret
 * message m to its partner, encrypted with the new session key k."
 *)
event ClientSendsMessageWithNewKey(ProcessID, Principal, ChatID, Principal, Principal, SessionKey, bitstring).
(*
 * ClientReceivesMsgWithNewKey(p, X, i, A, B, k, m):
 *
 * "Client X (one of A or B) running process p in rekeying session i has
 * received and decrypted a secret message m from its partner, encrypted with
 * the new session key k."
 *)
event ClientReceivesMessageWithNewKey(ProcessID, Principal, ChatID, Principal, Principal, SessionKey, bitstring).

(*
 * InitiatorSendsMsgWithNewKey(p, i, A, B, k, m):
 *
 * "Rekeying initiator A running process p in rekeying session i sends a secret
 * message m to B encrypted with the new session key k."
 *)
event InitiatorSendsMsgWithNewKey(ProcessID, ChatID, Principal, Principal, SessionKey, bitstring).

(*
 * InitiatorReceivesMsgWithNewKey(p, i, A, B, k, m):
 *
 * "Rekeying initiator A running process p in rekeying session i has received
 * and decrypted a secret message m from B, encrypted with the new session key k."
 *)
event InitiatorReceivesMsgWithNewKey(ProcessID, ChatID, Principal, Principal, SessionKey, bitstring).

(*
 * ResponderReceivesHalfKey(i, A, B, g_a):
 *
 * "Chat's responder B has received A's half-key g_a during rekeying session
 * i."
 *)
event ResponderReceivesHalfKey(ChatID, Principal, Principal, G).

(*
 * ResponderSendsHalfKey(i, A, B, g_b, fp):
 *
 * "Chat's responder B sends its half-key g_b and the fingerprint fp of the new
 * session key to A during rekeying session i."
 *)
event ResponderSendsHalfKey(ChatID, Principal, Principal, G, HashValue).

(*
 * ResponderReceivesAck(i, A, B, fp):
 *
 * "Chat's responder B has received the fingerprint fp of the new session key
 * negotiated with A during rekeying session i."
 *)
event ResponderReceivesAck(ChatID, Principal, Principal, HashValue).

(*
 * ResponderHasNegotiatedNewKey(i, A, B, k):
 *
 * "Chat's responder B has computed a new session key k in chat session i,
 * which he believes to be shared with A."
 *)
event ResponderHasNegotiatedNewKey(ChatID, Principal, Principal, SessionKey).

(*
 * ResponderSendsMsgWithNewKey(p, i, A, B, k, m):
 *
 * "Rekeying responder B running process p in rekeying session i sends a secret
 * message m to A encrypted with the new session key k."
 *)
event ResponderSendsMsgWithNewKey(ProcessID, ChatID, Principal, Principal, SessionKey, bitstring).

(*
 * ResponderReceivesMsgWithNewKey(p, i, A, B, k, m):
 *
 * "Rekeying responder B running process p in rekeying session i has received
 * and decrypted a secret message m from A, encrypted with the new session key k."
 *)
event ResponderReceivesMsgWithNewKey(ProcessID, ChatID, Principal, Principal, SessionKey, bitstring).


(******************************************************************************)
(* Process macros                                                             *)
(******************************************************************************)

let RekeyingInitiator(server: Principal) =
  (* An (honest) initiator must be one of Alice or Bob *)
  in(io, (X: Principal, rUser: Principal));
  let iUser = choosePrincipal(X, Alice, Bob) in

  (* See secret chat's protocol *)
  new iPID: ProcessID;

  (*
   * Retrieve the DH parameters and the session key used during the first
   * secret chat.
   *)
  get SecretChatClientTable(=iUser, =rUser, p, g, old_a, sessionKey) in

  (*
   * Retrieve the client's authorization key to communicate with the server.
   *)
  get AuthKeyClientTable(=iUser, =server, authKey) in

  (* 1. Generate random session id and DH half-key *)
  new a: Z;
  let g_a = dhExp(p, g, a) in
  new exchangeID: ChatID;

  event InitiatorSendsHalfKey(exchangeID, iUser, rUser, g_a);

  out(io, encodeSecretChatMsg(HALFKEYA(exchangeID, g_a), sessionKey, authKey));

  in(io, encMsg: Message);
  let HALFKEYB(=exchangeID, g_b, fp) = decodeSecretChatMsg(encMsg, sessionKey, authKey) in

  event InitiatorReceivesHalfKey(exchangeID, iUser, rUser, g_b, fp);

  let newKey = group2sess(dhExp(p, g_b, a)) in

  event RekeyingKeyFingerprintMatches(exchangeID, iUser, rUser, fp = fps(newKey));
  event InitiatorHasNegotiatedNewKey(exchangeID, iUser, rUser, newKey);
  event InitiatorSendsAck(exchangeID, iUser, rUser, fp);

  out(io, encodeSecretChatMsg(FPACK(exchangeID, fp), sessionKey, authKey));

  (* End of the rekeying protocol *)

  (* Store the secrets for use when testing observational equivalences. *)
  insert RekeyingClientTable(iUser, rUser, p, g, a, newKey);

  (* Now exchange a couple of messages with the new key *)

  event InitiatorSendsMsgWithNewKey (iPID,        exchangeID, iUser, rUser, newKey, iRekeyingText);
  event ClientSendsMessageWithNewKey(iPID, iUser, exchangeID, iUser, rUser, newKey, iRekeyingText);

  out(io, encodeSecretChatMsg(CHAT(iPID, iRekeyingText), newKey, authKey));

  in(io, rAnswer: Message);

  let CHAT(pid, rAnswerText) = decodeSecretChatMsg(rAnswer, newKey, authKey) in

  if pid <> iPID then

    event InitiatorReceivesMsgWithNewKey (iPID,        exchangeID, iUser, rUser, newKey, rAnswerText);
    event ClientReceivesMessageWithNewKey(iPID, iUser, exchangeID, iUser, rUser, newKey, rAnswerText);
    0.


let RekeyingResponder(server: Principal) =
  (* An (honest) responder must be one of Alice or Bob *)
  in(io, X: Principal);
  let rUser = choosePrincipal(X, Alice, Bob) in

  (* See secret chat's protocol *)
  new rPID: ProcessID;

  (*
   * Retrieve the DH parameters and the session key used during the first
   * secret chat.
   *)
  get SecretChatClientTable(iUser, =rUser, p, g, old_b, sessionKey) in

  (*
   * Retrieve the client's authorization key to communicate with the server.
   *)
  get AuthKeyClientTable(=rUser, =server, authKey) in

  in(io, encMsg1: Message);
  let HALFKEYA(exchangeID, g_a) = decodeSecretChatMsg(encMsg1, sessionKey, authKey) in

  event ResponderReceivesHalfKey(exchangeID, iUser, rUser, g_a);

  new b: Z;
  let newKey = group2sess(dhExp(p, g_a, b)) in
  let g_b = dhExp(p, g, b) in
  let fp = fps(newKey) in

  event ResponderSendsHalfKey(exchangeID, iUser, rUser, g_b, fp);

  out(io, encodeSecretChatMsg(HALFKEYB(exchangeID, g_b, fp), sessionKey, authKey));

  in(io, encMsg2: Message);
  let FPACK(=exchangeID, =fp) = decodeSecretChatMsg(encMsg2, sessionKey, authKey) in

  event ResponderReceivesAck(exchangeID, iUser, rUser, fp);
  event ResponderHasNegotiatedNewKey(exchangeID, iUser, rUser, newKey);

  (* End of the rekeying protocol *)

  (* Store the secrets for use when testing observational equivalences. *)
  insert RekeyingClientTable(iUser, rUser, p, g, b, newKey);

  (* Now exchange a couple of messages with the new key *)

  event ResponderSendsMsgWithNewKey (rPID,        exchangeID, iUser, rUser, newKey, rRekeyingText);
  event ClientSendsMessageWithNewKey(rPID, rUser, exchangeID, iUser, rUser, newKey, rRekeyingText);

  out(io, encodeSecretChatMsg(CHAT(rPID, rRekeyingText), newKey, authKey));

  in(io, iAnswer: Message);

  let CHAT(pid, iAnswerText) = decodeSecretChatMsg(iAnswer, newKey, authKey) in

  if pid <> rPID then

    event ResponderReceivesMsgWithNewKey (rPID,        exchangeID, iUser, rUser, newKey, iAnswerText);
    event ClientReceivesMessageWithNewKey(rPID, rUser, exchangeID, iUser, rUser, newKey, iAnswerText);
    0.

