(*
 * MTProto 2.0: Rekeying
 *
 * Model for the protocol used for updating the session keys of secret chats.
 *
 * See: https://core.telegram.org/api/end-to-end/pfs
 *
 * Expected time to run this script: ~30s
 *
 * NOTE: uncommenting RegisterSessionKey() and the related events in the query
 * increases the verification time to ~1h30m, but the query is still proved
 * true.
 *)

(*
 * If an (honest) client X receives a message m encrypted with the new session
 * key k after a rekeying session i has completed, and an (honest) client has
 * sent message m encrypted with a new session key k' after a rekeying session
 * i', then the session and the key coincide.
 *)

set preciseActions = true.

query x, x': Z, i: ChatID, X, Y, I, R: Principal, k, k', kx: SessionKey, m: bitstring;
  event(ClientSendsMessageWithNewKey(x', Y, i, I, R, k', m))
  && event(ClientReceivesMessageWithNewKey(x, X, i, I, R, k, m))
  ==> k = k'
  (* || event(AttackerRegisteredSessionKey(kx, X)) *)
  (* || event(AttackerRegisteredSessionKey(kx, Y)) *)
  || k = group2sess(BAD)
  || k' = group2sess(BAD).
(* ✔︎ Expected result: true *)


(* Main *)

let Init() =
  new authKey1: AuthKey;
  new authKey2: AuthKey;
  insert AuthKeyClientTable(Alice, Server, authKey1);
  insert AuthKeyClientTable(Bob, Server, authKey2);
  new a: Z;
  new b: Z;
  new i: ChatID;
  let k = genStrongSessionKey(a,b) in
  insert SecretChatClientTable(Alice, Alice, Bob, STRONG, GOOD, a, i, k);
  insert SecretChatClientTable(Bob, Alice, Bob, STRONG, GOOD, b, i, k);
  insert SecretChatClientTable(Alice, Alice, Bob, STRONG, BAD, a, i, k);
  insert SecretChatClientTable(Bob, Alice, Bob, STRONG, BAD, b, i, k);
  0.

process
  (0
    | !Init()
    (* | !RegisterSessionKey() *)
    | !RekeyingInitiator(Server)
    | !RekeyingResponder(Server)
    | !RekeyingSend(Server)
    | !RekeyingReceive(Server)
    | !RevealRekeyingInitiatorEphemeralKey()
    | !RevealRekeyingResponderEphemeralKey()
  )

